/*
 * Copyright 2020 Oscar David Gallon Rosero
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 *
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'dagger.hilt.android.plugin'
}

apply from: '../gradlescripts/signing.gradle'
apply from: '../gradlescripts/tests.gradle.kts'
apply from: '../gradlescripts/citask.gradle.kts'
apply from: '../gradlescripts/release.gradle'

android {
    compileSdkVersion appCompileSdkVersion
    buildToolsVersion appBuildToolsVersion

    defaultConfig {
        applicationId "com.oscarg798.amiibowiki"
        minSdkVersion appMinSdkVersion
        targetSdkVersion appTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        buildConfigField "String", "BASE_AMIIBO_API_URL", "\"$baseAmiiboUrl\""
        buildConfigField "String", "BASE_GAME_API_URL", "\"$baseGameUrl\""
        buildConfigField "String", "GAME_API_KEY", "\"$gameAPIKey\""
        buildConfigField "String", "GOOGLE_API_KEY", "\"$googleAPIKey\""
    }

    buildTypes {
        release {
            buildConfigField "Boolean", "ALPHA", "false"

            signingConfig signingConfigs.locker

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            buildConfigField "Boolean", "ALPHA", "false"

            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "AmiiboWiki-Debug"]

            minifyEnabled false
        }

        alpha {
            buildConfigField "Boolean", "ALPHA", "true"

            versionNameSuffix "-ALPHA"

            manifestPlaceholders = [appName: "AmiiboWiki-Alpha"]

            signingConfig signingConfigs.locker

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        freeCompilerArgs = ["-Xallow-result-return-type", "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi", "-Xopt-in=kotlinx.coroutines.FlowPreview", "-Xopt-in=kotlin.contracts.ExperimentalContracts"]
        jvmTarget = "1.8"
    }

    viewBinding {
        enabled = true
    }

    packagingOptions {
        exclude "**/attach_hotspot_windows.dll"
        exclude 'kotlin/coroutines/coroutines.kotlin_builtins'
        exclude 'kotlin/kotlin.kotlin_builtins'
        exclude 'kotlin/internal/internal.kotlin_builtins'
        exclude 'kotlin/ranges/ranges.kotlin_builtins'
        exclude 'kotlin/reflect/reflect.kotlin_builtins'
        exclude 'kotlin/collections/collections.kotlin_builtins'
        exclude 'kotlin/annotation/annotation.kotlin_builtins'
        exclude 'META-INF/**/*'
        exclude 'META-INF/*'
    }
}

dependencies {
    implementation(kotlinJDK)

    implementation appCompat
    implementation cardView
    implementation recyclerView
    implementation design
    implementation constraintLayout
    implementation appCompatV4
    implementation multidex

    implementation airbnb
    kapt airbnbProcessor

    //Dagger
    implementation dagger
    implementation daggerAndroidSupport
    kapt daggerCompiler
    kapt daggerAndroidProcessor

    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02"
    implementation hiltLib
    kapt hiltKAPT

    //Coroutines
    implementation kotlinCoroutines
    implementation kotlinCoroutinesAndroid

    implementation retrofit
    implementation gsonConverter
    implementation okhttp3
    implementation okhttpLoggingInterceptor

    implementation viewModelScope
    implementation lifecycleExtensions
    implementation lifecycle

    implementation lottie

    implementation flagly

    kapt project(':loggerprocessor')

    implementation roomRuntime
    implementation roomCompiler
    implementation roomKTX

    //Test
    testImplementation mockk
    testImplementation junit
    testImplementation kluent
    testImplementation kotlinCoroutinesTest

    //Aceptance Testing
    androidTestImplementation androidAnnotations
    androidTestImplementation mockWebServer
    androidTestImplementation testRunner
    androidTestImplementation testRules
    androidTestImplementation espressoCore
    androidTestImplementation espressoContrib
    androidTestImplementation espressoIntents
    androidTestImplementation uiAutomator
    androidTestImplementation mockk

    implementation firebaseAnalytics
    implementation crashly
    implementation(lomeno)

    implementation project(path: ':logger')
    implementation project(path: ':core')
    implementation project(path: ':amiibolist')
    implementation project(path: ':amiibodetail')
    implementation project(path: ':nfcreader')
    implementation project(path: ':network')
    implementation project(path: ':gamedetail')
    implementation project(path: ':settings')

    testImplementation project(path: ':testutils')
}
